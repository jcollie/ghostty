//! Native drag and drop for programs running in the terminal, independent
//! of the protocol a program uses to take part in it. The embedder
//! connects these to the OS; Kitty's OSC 72 (`kitty.dnd`) is the only
//! protocol today.

const std = @import("std");
const Allocator = std.mem.Allocator;

/// A drag and drop operation.
///
/// C: GhosttyDndOperation
pub const Operation = enum(c_int) {
    none = 0,
    copy = 1,
    move = 2,
};

/// A machine's identity, for telling whether a program is running on
/// the machine the terminal is on: `1:` and the hex HMAC-SHA256 of the
/// OS machine ID, keyed with "tty-dnd-protocol-machine-id". Kitty's drag
/// and drop protocol compares it to the one a program declares.
pub const MachineId = [66]u8;

/// The machine ID for a raw OS machine ID: the contents of
/// /etc/machine-id on Linux and the BSDs, IOPlatformUUID on macOS, or
/// MachineGuid on Windows. Trailing whitespace is ignored.
pub fn machineId(raw: []const u8) MachineId {
    const Hmac = std.crypto.auth.hmac.sha2.HmacSha256;
    var mac: [Hmac.mac_length]u8 = undefined;
    Hmac.create(&mac, std.mem.trimEnd(u8, raw, &std.ascii.whitespace), "tty-dnd-protocol-machine-id");
    var id: MachineId = undefined;
    id[0..2].* = "1:".*;
    id[2..].* = std.fmt.bytesToHex(mac, .lower);
    return id;
}

/// What a file entry in a drop is, as the embedder reports it before its
/// data. Symbolic links are never followed.
///
/// C: GhosttyDropFileKind
pub const FileKind = enum(c_int) {
    /// A regular file; the data is its contents.
    file = 0,

    /// A symbolic link; the data is its target.
    symlink = 1,

    /// A directory; the data is the names of its entries that are
    /// regular files, directories or symbolic links, each followed by a
    /// NUL byte.
    directory = 2,
};

/// The set of operations a drag allows.
///
/// C: GhosttyDndOperations
pub const Operations = packed struct(u32) {
    copy: bool = false,
    move: bool = false,
    _padding: u30 = 0,
};

/// A list of MIME types, borrowed from the terminal and only valid for
/// the duration of the effect callback it was delivered to. MIME types
/// can't contain the separator, so the list is held as received rather
/// than split into slices.
pub const MimeList = struct {
    bytes: []const u8 = "",
    separator: u8 = ' ',

    pub fn iterator(self: MimeList) std.mem.TokenIterator(u8, .scalar) {
        return std.mem.tokenizeScalar(u8, self.bytes, self.separator);
    }

    pub fn dupe(self: MimeList, alloc: Allocator) Allocator.Error!MimeList {
        return .{ .bytes = try alloc.dupe(u8, self.bytes), .separator = self.separator };
    }

    pub fn count(self: MimeList) usize {
        var it = self.iterator();
        var n: usize = 0;
        while (it.next()) |_| n += 1;
        return n;
    }
};

/// A change in drops onto the terminal that the embedder may need to act
/// on. Everything borrowed is only valid for the duration of the effect
/// callback.
pub const DropEvent = union(enum) {
    /// The program started or stopped accepting drops. While it accepts
    /// them, native drags over the terminal go to it rather than being
    /// handled as they would be without it (e.g. pasting dropped paths).
    registration: Registration,

    /// The program answered the drag over the terminal, for the OS drag
    /// feedback. Until it accepts, the drag isn't accepted: a drop it
    /// hasn't accepted would never be read or concluded, so the embedder
    /// refuses it.
    acceptance: Acceptance,

    /// The program wants data from the drop, which the embedder reads
    /// from the native drop (asynchronously if it must) and sends back.
    data_request: DataRequest,

    /// The program is done with the drop. The embedder finishes the
    /// native drop with the operation it performed.
    concluded: Operation,

    /// A copy of the event that owns everything it borrows, allocated
    /// with `alloc` (typically an arena), for delivering it somewhere the
    /// terminal's state can't be borrowed from.
    pub fn dupe(self: DropEvent, alloc: Allocator) Allocator.Error!DropEvent {
        return switch (self) {
            .registration => |r| .{ .registration = .{
                .accepting = r.accepting,
                .mimes = try r.mimes.dupe(alloc),
            } },
            .acceptance => |a| .{ .acceptance = .{
                .operation = a.operation,
                .mimes = try a.mimes.dupe(alloc),
            } },
            .data_request => |r| .{ .data_request = .{
                .id = r.id,
                .mime_index = r.mime_index,
                .mime = try alloc.dupe(u8, r.mime),
                .path = if (r.path) |path| try alloc.dupe(u8, path) else null,
            } },
            .concluded => self,
        };
    }

    pub const Registration = struct {
        accepting: bool,

        /// MIME types the program declared it accepts, if any. Only
        /// needed to register types with the OS ahead of a drag.
        mimes: MimeList = .{},
    };

    pub const Acceptance = struct {
        /// The operation the program would perform, or none if it
        /// rejects the drag.
        operation: Operation,

        /// The MIME types it wants, most preferred first. Empty when
        /// the program didn't say.
        mimes: MimeList = .{},
    };

    pub const DataRequest = struct {
        /// Identifies the request when answering it. Never reused, so an
        /// answer to a request the program abandoned is rejected rather
        /// than answering another.
        id: u32,

        /// Index into the MIME types of the drop.
        mime_index: u32,

        /// The MIME type to read from the native drop. Empty for a file
        /// request.
        mime: []const u8,

        /// For a file request, the absolute path of a file the drop
        /// named (in its text/uri-list) or one inside a directory it
        /// named, for a program on another machine to copy. The embedder
        /// reports what it is with `DropInput.kind`, without following
        /// symbolic links, then sends its data. Null for a MIME request.
        path: ?[]const u8 = null,
    };
};

/// A change in the drag the program offers out of the terminal that the
/// embedder may need to act on. Everything borrowed is only valid for the
/// duration of the effect callback.
pub const DragEvent = union(enum) {
    /// The program started or stopped offering drags. While it offers
    /// them, the platform's drag gesture over the terminal goes to it so
    /// it can offer a drag, rather than being handled as it would be
    /// without it (e.g. selecting text).
    offers: bool,

    /// The program asked to start a drag. The embedder copies what it
    /// needs of the offer, starts the native drag, and reports whether
    /// it started.
    start: Offer,

    /// The program changed the image of the started drag to the image at
    /// this index of the offer, or to no image.
    image: ?u32,

    /// Data a drop target wanted that the embedder requested from the
    /// program arrived or failed.
    data: Data,

    /// The native drag in progress must be canceled.
    cancel,

    /// A copy of the event that owns everything it borrows, allocated
    /// with `alloc` (typically an arena), for delivering it somewhere the
    /// terminal's state can't be borrowed from.
    pub fn dupe(self: DragEvent, alloc: Allocator) Allocator.Error!DragEvent {
        return switch (self) {
            .offers, .image, .cancel => self,
            .start => |offer| start: {
                const items = try alloc.alloc(Offer.Item, offer.items.len);
                for (items, offer.items) |*item, src| item.* = .{
                    .mime = try alloc.dupe(u8, src.mime),
                    .pre_sent = if (src.pre_sent) |data| try alloc.dupe(u8, data) else null,
                };
                const images = try alloc.alloc(Image, offer.images.len);
                for (images, offer.images) |*image, src| {
                    image.* = src;
                    image.data = try alloc.dupe(u8, src.data);
                }
                break :start .{ .start = .{
                    .operations = offer.operations,
                    .items = items,
                    .images = images,
                    .image = offer.image,
                } };
            },
            .data => |data| .{ .data = .{
                .index = data.index,
                .bytes = try alloc.dupe(u8, data.bytes),
                .status = data.status,
            } },
        };
    }

    pub const Offer = struct {
        /// The operations the drag allows.
        operations: Operations,

        /// The MIME types offered, in order. Data for a type that
        /// wasn't pre-sent is requested from the program during the drag.
        items: []const Item,

        /// The images the program supplied for the drag, if any.
        images: []const Image,

        /// The index of the image to show, or null for no image.
        image: ?u32,

        pub const Item = struct {
            mime: []const u8,

            /// The data the program sent ahead of the drag, if any.
            pre_sent: ?[]const u8 = null,
        };
    };

    pub const Data = struct {
        /// Index into the offer's items.
        index: u32,

        /// Data received since the last event for this item.
        bytes: []const u8,

        status: Status,

        /// C: GhosttyDragDataStatus
        pub const Status = enum(c_int) {
            /// More data may follow.
            pending = 0,

            /// All data has been received.
            complete = 1,

            /// The program failed to provide the data.
            failed = 2,
        };
    };
};

/// A drag image.
pub const Image = struct {
    format: Format,

    /// Width in pixels, or for text the numerator of the font size scale
    /// (zero meaning one).
    width: u32,

    /// Height in pixels, or for text the denominator of the font size
    /// scale (zero meaning one).
    height: u32,

    /// Background opacity for text, 0 (transparent) through 1024
    /// (opaque).
    opacity: u32,

    data: []const u8,

    /// C: GhosttyDragImageFormat
    pub const Format = enum(c_int) {
        /// 32-bit RGBA pixels.
        rgba = 0,

        /// A PNG image, for the embedder to decode.
        png = 1,

        /// UTF-8 text for the embedder to render as the image.
        text = 2,
    };
};

/// A position on the terminal.
pub const Position = struct {
    /// Grid cell, zero-based from the top-left.
    cell_x: u32,
    cell_y: u32,

    /// Pixels relative to the top-left of the terminal's content area.
    pixel_x: i32,
    pixel_y: i32,
};

/// Native drop activity the embedder reports.
pub const DropInput = union(enum) {
    /// A native drag entered or moved over the terminal.
    move: Motion,

    /// The native drag left the terminal without dropping.
    leave,

    /// The native drag dropped onto the terminal. The embedder keeps the
    /// native drop open to serve data requests until it is concluded.
    drop: Motion,

    /// What the file of the file request being served is, before any of
    /// its data. A file request not reported is a regular file.
    kind: Kind,

    /// Some of the data for the data request being served, sent as given.
    data: Data,

    /// The data request being served is complete.
    end: u32,

    /// The data request being served failed.
    fail: Failure,

    pub const Motion = struct {
        position: Position,

        /// The operations the drag allows.
        operations: Operations,

        /// The MIME types of the drag, which data requests index.
        mimes: []const []const u8,
    };

    pub const Data = struct {
        id: u32,
        bytes: []const u8,
    };

    pub const Failure = struct {
        id: u32,
        reason: Error,
    };

    pub const Kind = struct {
        id: u32,
        kind: FileKind,
    };

    /// Why reading drop data failed.
    ///
    /// C: GhosttyDropError
    pub const Error = enum(c_int) {
        io = 0,
        not_found = 1,
        denied = 2,
        too_large = 3,
        out_of_memory = 4,

        /// A file request named something other than a regular file,
        /// directory or symbolic link.
        unsupported = 5,
    };
};

/// Native drag activity the embedder reports for a drag the program
/// offers.
pub const DragInput = union(enum) {
    /// The user started the platform's drag gesture over the terminal.
    gesture: Position,

    /// The result of starting the native drag the program asked for.
    start_result: StartResult,

    /// A drop target accepted the drag, preferring the offered MIME type
    /// at this index if known.
    accepted: ?u32,

    /// The operation the drag would perform changed.
    operation: Operation,

    /// The drag was dropped onto a target.
    dropped,

    /// The drag finished, which ends it. True if it was canceled.
    finished: bool,

    /// A drop target wants data for the offered MIME type at this index
    /// that wasn't pre-sent.
    request_data: u32,

    /// C: GhosttyDragStartResult
    pub const StartResult = enum(c_int) {
        /// The native drag started.
        started = 0,

        /// The user already let go of the drag.
        denied = 1,

        /// The native drag couldn't be started.
        failed = 2,
    };
};

test "machine ID" {
    // Computed with kitty's machine_id.py algorithm.
    const id = machineId("0123456789abcdef0123456789abcdef\n");
    try std.testing.expectEqualStrings(
        "1:a81b6c3c9d37b0caa4a9c6b7de41059238bc101829db2975223b93d39d663b08",
        &id,
    );
}

test "MimeList iterates either separator" {
    const testing = std.testing;
    const spaced: MimeList = .{ .bytes = "text/plain image/png " };
    try testing.expectEqual(@as(usize, 2), spaced.count());
    const nul: MimeList = .{ .bytes = "text/plain\x00", .separator = 0 };
    var it = nul.iterator();
    try testing.expectEqualStrings("text/plain", it.next().?);
    try testing.expect(it.next() == null);
    try testing.expectEqual(@as(usize, 0), (MimeList{}).count());
}
